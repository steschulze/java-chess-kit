plugins {
    id 'java-library'
    id 'jacoco'
    id 'checkstyle'
}

group 'de.stefanet'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories = (files(classDirectories.files.collect {
            fileTree(dir: it,
                    exclude: ['de/stefanet/javachesskit/examples/**'])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'METHOD'
                minimum = 1.0
            }
            excludes = ['de.stefanet.javachesskit.examples.*']
        }
    }
}

checkstyle {
    toolVersion = "9.3"
    configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
}

javadoc {
    options.encoding = 'UTF-8'
}